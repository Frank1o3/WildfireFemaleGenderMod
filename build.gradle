plugins {
	id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
	id 'maven-publish'
}

version = "fabric-${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
	maven { url = "https://maven.terraformersmc.com/" }
	maven { url = "https://maven.parchmentmc.org/" }
	mavenCentral()
}

loom {
	splitEnvironmentSourceSets()

	accessWidenerPath = file("src/main/resources/wildfire_gender.accesswidener")

	mods {
		"wildfirefemalegendermod" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		if(!project.parchment_version.isEmpty()) {
			parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
		}
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation fabricApi.module("fabric-networking-api-v1", project.fabric_api_version)
	modImplementation fabricApi.module("fabric-key-binding-api-v1", project.fabric_api_version)
	modImplementation fabricApi.module("fabric-lifecycle-events-v1", project.fabric_api_version)
	modImplementation fabricApi.module("fabric-command-api-v2", project.fabric_api_version)
	modImplementation fabricApi.module("fabric-rendering-v1", project.fabric_api_version)
	modImplementation fabricApi.module("fabric-resource-loader-v1", project.fabric_api_version)
	modRuntimeOnly fabricApi.module("fabric-registry-sync-v0", project.fabric_api_version)

	modLocalRuntime "me.djtheredstoner:DevAuth-fabric:${project.devauth_version}"

	localRuntime "org.apache.httpcomponents:httpclient:4.5.14"
	localRuntime "org.apache.httpcomponents:httpcore:4.4.16"

	modCompileOnly("com.terraformersmc:modmenu:${project.modmenu_version}")
}

processResources {
	inputs.property "version", project.mod_version
	inputs.property "minecraft_version", project.minecraft_version
	inputs.property "minecraft_dependency", project.minecraft_dependency

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version,
				"minecraft_version": project.minecraft_version,
				"minecraft_dependency": project.minecraft_dependency
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}